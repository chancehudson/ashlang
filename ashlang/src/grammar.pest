/*
    *
    * This file is organized top to bottom in order
    * of decreasing specificity. Similar types are
    * grouped near each other when possible.
*/

program = _{ SOI ~ "\n"* ~ (fn_header ~ "\n")? ~ "\n"* ~ (stmt ~ "\n"+)* ~ (return_stmt ~ "\n"?)? ~ "\n"* ~ EOI }

fn_header = { "(" ~ ((varname ~ ("," | ")"))+ | ")") }
stmt      = { var_def | if_stmt | function_call | precompile_stmt | var_index_assign | var_vec_def }

expr        = { atom ~ (op ~ atom)* }
return_stmt = { "return " ~ expr }

// this is distinct from fn_header because it accepts an expr or a var
fn_args       = { "(" ~ ((expr ~ ("," | ")"))+ | ")") }
function_call = { varname ~ fn_args }

// precompiles that return values

precompile_with_output = { "read_input" }
precompile_no_output = { "loop" | "write_output" | "assert_eq" }
precompile_stmt = { precompile_no_output ~ expr+ ~ block? }
precompile_expr = { precompile_with_output ~ expr+ ~ block? }

op        = _{ add | sub | mul | inv }
add       =  { "+" }
sub       =  { "-" }
mul       =  { "*" }
inv       =  { "/" }
bool_op   = _{ equal | not_equal | gt | lt }
equal     =  { "==" }
not_equal =  { "!=" }
gt        =  { ">" }
lt        =  { "<" }

bool_expr = { expr ~ bool_op ~ expr }
if_stmt   = { "if " ~ bool_expr ~ block }
block     = { "{" ~ "\n"* ~ ((stmt ~ "\n") | "\n")* ~ "\n"* ~ "}" }

vec = { "[" ~ "\n"* ~ (vec | literal_dec) ~ "\n"* ~ ("\n"* ~ "," ~ "\n"* ~ (vec | literal_dec))* ~ "\n"* ~ "]" }

// this let_r match is needed to
// determine if a variable is being
// declared for the first time
var_def          = { var ~ "=" ~ (expr | vec) }
let_r            = { "let " }
static_r         = { "static " }
var_vec_def      = { (let_r | static_r) ~ var_indexed }
var_index_assign = { var_indexed ~ "=" ~ expr }

var         = { (let_r | static_r)? ~ varname }
var_indexed = { varname ~ ("[" ~ expr ~ "]")? }

atom = { ("0x" ~ literal_hex) | literal_dec | precompile_expr | function_call | var_indexed | varname }

literal_dec = @{ ASCII_DIGIT+ }
literal_hex = @{ ASCII_HEX_DIGIT+ }

varname = @{ char+ }

char = _{ ASCII_ALPHANUMERIC | "_" }

WHITESPACE = _{ " " }
COMMENT    = _{ "#" ~ (!"\n" ~ ANY)* }
